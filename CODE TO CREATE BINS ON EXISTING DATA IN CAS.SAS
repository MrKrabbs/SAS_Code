/* CUSTOMER WILL NOT NEED STEP BELOW, i DID THIS AS MY LOSS RATIO WAS SCREWED UP */
DATA CASUSER.STEP_A;
	SET PUBLIC.INSURANCE_DEMO_CS_P;
	CLAIM_AMOUNT=CLAIM_AMOUNT * .275;
RUN;

/* GRAB OUR SOURCE TABLE AND SIMPLY SUM PREMIUM AND LOSS AT A POLICY LEVEL DETAIL (INCLUDED POLICIES OVER MULTIPLE YEARS) */
/* WE ARE DOING THIS BECAUSE WE WANT A TABLE THAT WE CAN CREATE LOSS RATIO AT A POLICY LEVEL OF DETAIL*/
PROC CAS;
	aggregation.aggregate / varSpecs={{agg="SUMMARY", names={"CLAIM_AMOUNT", 
		"PREMIUM"}, summarySubset={"SUM"}}} table={caslib="casuser", name="STEP_A", 
		groupBy={"Policy_Number"}}, casout={caslib="casuser", name="STEP_B", 
		replace=true};
	run;
quit;

/* CREATE LOSS AND KEEP POLICY NUMBER */
DATA CASUSER.STEP_C;
	SET CASUSER.STEP_B;
	LOSS_RATIO=_Claim_Amount_Summary_Sum_ / _PREMIUM_Summary_Sum_;
	KEEP LOSS_RATIO Policy_Number;
RUN;

/* ATTACH A DECILE BIN*/
proc binning data=CASUSER.STEP_C numbin=10 method=quantile;
	input LOSS_RATIO;
	output out=CASUSER.TEN_BINS copyvars=(_all_);
run;

/* RENAME BIN_LOSS_RATIO SO WE CAN BIN AGAIN */
DATA CASUSER.SEND2_5BIN;
	SET CASUSER.TEN_BINS;
	RENAME BIN_LOSS_RATIO=LR_DECILES;
RUN;

/* ATTACH A QUINTILE BIN*/
proc binning data=CASUSER.SEND2_5BIN numbin=5 method=quantile;
	input LOSS_RATIO;
	output out=CASUSER.FIVE_BINS copyvars=(_all_);
run;

/* RENAME BIN_LOSS_RATIO SO WE CAN BIN AGAIN */
DATA CASUSER.SEND2_20BIN;
	SET CASUSER.FIVE_BINS;
	RENAME BIN_LOSS_RATIO=LR_QUINTILES;
RUN;

/* ATTACH A VENTILE BIN */
proc binning data=CASUSER.SEND2_20BIN numbin=20 method=quantile;
	input LOSS_RATIO;
	output out=CASUSER.TWENTY_BINS copyvars=(_all_);
run;

/* CREATE A DUPLICATE OF BIN VALUE IN A TEXT FORMAT. DEPENDING ON VISUALIZATION, TEXT OR NUMERIC BINS WILL HAVE AN ADVANTAGE */
DATA CASUSER.BIN_DATA_READY2_JOIN;
	SET CASUSER.TWENTY_BINS;
	LENGTH LR_DEC_TXT LR_QUIN_TXT LR_VEN_TXT $2.;
	LR_DEC_TXT=PUT(LR_DECILES, Z2.);
	LR_QUIN_TXT=PUT(LR_QUINTILES, Z2.);
	LR_VEN_TXT=PUT(BIN_LOSS_RATIO, Z2.);
	RENAME BIN_LOSS_RATIO=LR_VENTILES;
	KEEP POLICY_NUMBER LR_DECILES LR_QUINTILES BIN_LOSS_RATIO LOSS_RATIO 
		LR_DEC_TXT LR_QUIN_TXT LR_VEN_TXT;
RUN;

/* DROP OUR TABLE BEFORE WE CREATE IT IN FEDSQL, IF TABLE DOES NOT EXIST- NOT AN ISSUE DUE TO THE QUIET OPTION USED */
proc casutil;
	droptable incaslib="CASUSER" casdata="CS_P_W_QUANTILES" quiet;
run;

/* TAKE OUR ORIGINAL DATA AND TAG LOSS RATIO AND 3 DIFFERENT BIN GROUPINGS( OF LOSS RATIO) */
PROC FEDSQL SESSREF=CASAUTO;
	CREATE TABLE CASUSER.CS_P_W_QUANTILES {options replace=true copies=0} AS 
		SELECT A.*, B.LR_DECILES, B.LR_QUINTILES, B.LR_VENTILES, B.LOSS_RATIO, 
		B.LR_DEC_TXT, B.LR_QUIN_TXT, B.LR_VEN_TXT FROM CASUSER.STEP_A A JOIN 
		CASUSER.BIN_DATA_READY2_JOIN B ON A.POLICY_NUMBER=B.POLICY_NUMBER;
	QUIT;

	/* PROMOTE OUR TABLE SO THAT WE CAN "SEE" IT OUTSIDE SAS STUDIO */
proc casutil outcaslib="CASUSER";
	promote casdata="CS_P_W_QUANTILES";
	quit;
